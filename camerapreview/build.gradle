apply plugin: 'com.android.library'
apply plugin: 'com.github.dcendents.android-maven'
apply plugin: 'com.jfrog.bintray' // bintray upload command : gradlew bintrayUpload

// This is the library version used when deploying the artifact
version = "0.0.8"

def siteUrl = 'https://github.com/kecb/CameraView'      // Homepage URL of the library
def gitUrl = 'https://github.com/kecb/CameraView.git'   // Git repository URL
group = "co.ilife.camerapreview"                                        // Maven Group ID for the artifact

android {
  compileSdkVersion 23
  buildToolsVersion "23.0.2"

  defaultConfig {
    minSdkVersion 14
    targetSdkVersion 23
    versionCode 1
    versionName "1.0"
  }
  buildTypes {
    release {
      minifyEnabled false
      proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
    }
  }
}

Properties properties = new Properties()
properties.load(project.rootProject.file('local.properties').newDataInputStream())

bintray {
  user = properties.getProperty("bintray.user")
  key = properties.getProperty("bintray.apikey")

  configurations = ['archives']
  pkg {
    repo = "maven"
    name = "CameraView"
    websiteUrl = siteUrl
    vcsUrl = gitUrl
    licenses = ["Apache-2.0"]
    publish = true
  }
}

install {
  repositories.mavenInstaller {
    // This generates POM.xml with proper parameters
    pom {
      project {
        packaging 'aar'

        // Add your description here
        name 'A simple view to preview camera and record a video.'
        url siteUrl

        // Set your license
        licenses {
          license {
            name 'The Apache Software License, Version 2.0'
            url 'http://www.apache.org/licenses/LICENSE-2.0.txt'
          }
        }
        developers {
          developer {
            id 'kecb'
            name 'Beck'
            email '406761423@qq.com'
          }
        }
        scm {
          connection gitUrl
          developerConnection gitUrl
          url siteUrl

        }
      }
    }
  }
}



dependencies {
  compile fileTree(dir: 'libs', include: ['*.jar'])
  testCompile 'junit:junit:4.12'
  compile 'com.android.support:appcompat-v7:23.1.1'
}

task sourcesJar(type: Jar) {
  from android.sourceSets.main.java.srcDirs
  classifier = 'sources'
}

artifacts {
  archives sourcesJar
}

task makeJar(type: Copy) {
  delete 'build/libs/cameraview'+version+'.jar'
  from('build/intermediates/bundles/release/')
  into('build/libs/')
  include('classes.jar')
  rename ('classes.jar', 'cameraview'+version+'.jar')
}
